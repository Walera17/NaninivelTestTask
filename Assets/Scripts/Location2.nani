@back Loc2 time:1
@wait 0.5
@char Jody look:Left pos:75 scale:4.8

@wait 0.5
;@printer Wide
ElenNovel: Привет. Меня зовут {name} и мне приятно с тобой познакомиться. Ты знаешь, где достать меч.

@set triesSword=0
@set triesHelmet=0
@set pathHelmet=false
@wait 0.5

@printer Bubble.Right pos:52,68
Jody: Меня зовут Jody и мне приятно с тобой познакомиться. Да я покажу тебе, где ты сможешь достать меч.
@hidePrinter
@choice handler:MyHandlerSword button:SwordGame pos:750,300 goto:.GotoLocation3
@stop

# JodyRepeatGetSword
@printer Bubble.Right pos:52,68
Jody: Ты не достал меч. Иди и принеси меч.
@hidePrinter
@choice handler:MyHandlerSword button:SwordGame pos:750,300 goto:.GotoLocation3
@stop

# JodyGetHelmet
@printer Bubble.Right pos:52,68
Jody: Ты достал меч. Молодец. Мне нужен шлем. Я хорошо заплачу. Добудешь?

@wait 0.5
@printer Dialogue
ElenNovel: Хорошо.Я принесу тебе шлем.
@set pathHelmet=true
@char Helmet pos:8,95
@lock SuperHelmet

@printer Bubble.Right pos:52,68
Jody: Вот место, где ты сможешь достать шлем
@hidePrinter
@choice handler:MyHandlerHelmet button:HelmetGame pos:750,-50 goto:.GotoLocation3
@stop

# JodyRepeatGetHelmet
@printer Bubble.Right pos:52,68
Jody: Ты не достал шлем. Иди и принеси шлем. 
@hidePrinter
@choice handler:MyHandlerHelmet button:HelmetGame pos:750,-50 goto:.GotoLocation3
@stop

# Choices
@back Loc2.RadialBlur time:1
@set ChoiceVisible=true
@show Jody 

@goto .JodyRepeatGetSword if:!RewardExist("Sword")
@goto .JodyGetHelmet if:"!RewardExist("Helmet") && !pathHelmet"
@goto .JodyRepeatGetHelmet if:"!RewardExist("Helmet") && pathHelmet"
@goto .Finish if:RewardExist("Helmet")

# GotoLocation3
@set ChoiceVisible=false
@hidePrinter
@hide Jody
@goto Location3.GetWeapon

# Finish
@printer Dialogue
@print text:"Все задачи выполнены."
@hidePrinter 
@set ChoiceVisible=false
@goto Location1.FinishDialog

@stop